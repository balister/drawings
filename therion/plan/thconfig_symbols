encoding  utf-8
layout symbols

 # symbol-set UIS 

  code metapost
     let p_gradient = p_entrance_UIS;
     let p_clay = p_flowstone_UIS;
     let l_wall_debris = l_wall_debris_AUT;
     let l_wall_blocks = l_wall_blocks_AUT;
 
  beginpattern(pattern_water);
      draw origin--10up withpen pensquare scaled (0.03u); % line thickness
      patternxstep(.5u);                                 % spacing
      patterntransform(identity rotated 45);
  endpattern;
  
    def a_water (expr Path) =
    T:=identity;
    thclean Path;
    pickup PenD;
    thfill Path withpattern pattern_water;  
  enddef;
  
      beginpattern(pattern_mud);
        pickup PenC;
        p:=(.0u,.0u) -- (0.25u,.0u);
        for i=0.0u step 0.6u until 5.4u:
            for j=0.0u step 0.6u until 5.4u:
                draw p shifted ((i,j) randomized 0.2u);
                draw p shifted ((i+.3u,j+.3u) randomized 0.2u);
            endfor;
        endfor;
        patternstep(5.4u,5.4u);
    endpattern;

    def a_clay (expr p) =
        T:=identity;
        thfill p withpattern pattern_mud;
    enddef;
       
       beginpattern(pattern_sand);
        pickup PenC;
        p:= origin -- (0.01u,0.01u);
        for i=0.0u step 0.5u until 2.5u:  %AUT is step 0.2u 
          for j=0.0u step 0.5u until 2.5u:  %AUT is step 0.2u 
            draw p rotated uniformdeviate(360) 
             shifted ((i,j) randomized 0.4u);  %%AUT is 0.09u
          endfor;
        endfor;
        if BaseScale<=2.5:
            my_step:=2.6u;  %mystep controls tessilation pattern, must synchronise with i, j above, was 2.4u
        else:
            my_step:=2.8u;  %was 2.6u
        fi;
        patternstep(my_step,my_step);       
    endpattern;

    def a_sand (expr Path) =
        T:=identity;
        %thclean Path; %makes passage colour more intense under area, not good for sand
        thfill Path withpattern pattern_sand ;
    enddef;

  # Adapted from Thomas Holder's
 def s_scalebar (expr l, units, txt) =
  begingroup
    interim warningcheck:=0;
    tmpl:=l / Scale * cm * units / 2;
    tmpx:=l / Scale * cm * units / 5;
    tmph:=5bp; % bar height
  endgroup;
  pickup PenC;
  draw (-tmpl,0)--(tmpl,0);
  p:=(0,0)--(0,-tmph);
  for i:=-2.5 step 1 until 2.5:
    draw p shifted (i * tmpx,0);
  endfor;
  begingroup
    interim labeloffset:=3.5bp;
    for i:=0 step (l/5) until (l-1):
      tmpx:=tmpl * (i * 2 / l - 1);
      label.bot(thTEX(decimal (i)),(tmpx,-tmph));
    endfor;
    label.bot(thTEX(decimal (l) & "\thinspace" & txt),(tmpl,-tmph));
    label.top(thTEX("Original Scale 1:" & decimal round(Scale*100)),(0,0));
  endgroup;
enddef; 
  
  endcode
 
endlayout

